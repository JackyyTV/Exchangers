plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

import net.minecraftforge.gradle.common.tasks.SignJar

version = "${mc_version}-${mod_version}"
group = "jackyy.${mod_id}"
base {
    archivesName = jar_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mappings_channel, version: mappings_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        client {
            if (project.hasProperty('mcUUID')) {
                args '--uuid', project.getProperty('mcUUID')
            }
            if (project.hasProperty('mcUsername')) {
                args '--username', project.getProperty('mcUsername')
            }
            if (project.hasProperty('mcAccessToken')) {
                args '--accessToken', project.getProperty('mcAccessToken')
            }
        }
        server {
            args '--nogui'
        }
        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'ModMaven'
        url = 'https://modmaven.dev'
    }
    maven {
        name = 'JEI Maven'
        url = 'https://dvs1.progwml6.com/files/maven'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        name = 'Curse Maven'
        url = 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("mezz.jei:jei-1.16.5:+")
    implementation fg.deobf("curse.maven:hwyla-253449:3033593") //1.10.11-B78_1.16.2

    implementation fg.deobf("curse.maven:gunpowderlib-356646:4515267") //1.16.5-1.2.3

    runtimeOnly fg.deobf("mekanism:Mekanism:1.16.5-+")
    //runtimeOnly fg.deobf("curse.maven:immersive_engineering-231951:3809596") //1.16.5-5.1.0-148
    runtimeOnly fg.deobf("curse.maven:cofh_core-69162:4022663") //1.16.5-1.5.2.22
    runtimeOnly fg.deobf("curse.maven:thermal_foundation-222880:4022666") //1.16.5-1.5.2.30
    runtimeOnly fg.deobf("curse.maven:thermal_innovation-291737:3788896") //1.16.5-1.5.0.4
    runtimeOnly fg.deobf("curse.maven:thermal_expansion-69163:4063134") //1.16.5-1.5.2.16
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mc_version: mc_version, mc_version_range: mc_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, jar_name: jar_name,
            mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": jar_name,
                "Specification-Version": "${mc_version}-${mod_version}",
                "Specification-Vendor": mod_authors,
                "Implementation-Title": jar_name,
                "Implementation-Version": "${mc_version}-${mod_version}",
                "Implementation-Vendor": mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

task signJar(type: SignJar, dependsOn: jar) {
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archiveFile
    outputFile = jar.archiveFile
}

build.dependsOn signJar

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveClassifier.set("deobf")
}

artifacts {
    archives sourcesJar
    archives deobfJar
}
