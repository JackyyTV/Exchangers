buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    id 'eclipse'
}
apply plugin: 'net.minecraftforge.gradle'

import net.minecraftforge.gradle.common.tasks.SignJar

version = "${mc_version}-${mod_version}"
group = 'jackyy.exchangers'
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            if (project.hasProperty('mcUUID')) {
                args '--uuid', project.getProperty('mcUUID')
            }
            if (project.hasProperty('mcUsername')) {
                args '--username', project.getProperty('mcUsername')
            }
            if (project.hasProperty('mcAccessToken')) {
                args '--accessToken', project.getProperty('mcAccessToken')
            }
            property 'forge.enabledGameTestNamespaces', 'exchangers'
            mods {
                exchangers {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'exchangers'
            mods {
                exchangers {
                    source sourceSets.main
                }
            }
        }
        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'exchangers'
            mods {
                exchangers {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'exchangers', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                exchangers {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'ModMaven'
        url = 'https://modmaven.dev'
    }
    maven {
        name = 'JEI Maven'
        url = 'https://dvs1.progwml6.com/files/maven'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        name = 'Curse Maven'
        url = 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("mezz.jei:jei-1.19.4-forge:+")
    //implementation fg.deobf("curse.maven:jade-324717:4538674") //1.19.4-10.3.0
    implementation fg.deobf("curse.maven:theoneprobe-245211:4442439") //1.19.4-8.0.0

    implementation fg.deobf("curse.maven:gunpowderlib-356646:4519993") //1.19.4-2.1

    //runtimeOnly fg.deobf("curse.maven:mekanism-268560:")
    //runtimeOnly fg.deobf("curse.maven:immersive_engineering-231951:4535969") //1.19.4-9.4.0-166
    //runtimeOnly fg.deobf("curse.maven:cofh_core-69162:")
    //runtimeOnly fg.deobf("curse.maven:thermal_foundation-222880:")
    //runtimeOnly fg.deobf("curse.maven:thermal_innovation-291737:")
    //runtimeOnly fg.deobf("curse.maven:thermal_expansion-69163:")
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Version": "${project.mc_version}-${project.mod_version}",
                "Specification-Vendor": "Jackyy",
                "Implementation-Title": "${mod_name}",
                "Implementation-Version": "${project.mc_version}-${project.mod_version}",
                "Implementation-Vendor" :"Jackyy",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar.finalizedBy('reobfJar')

task signJar(type: SignJar, dependsOn: jar) {
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourcesJar
    archives deobfJar
}
